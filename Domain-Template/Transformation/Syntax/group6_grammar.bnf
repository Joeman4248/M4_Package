USE_LR_PARSER


(*	Grammar for Group 6, Milestone 3
    Authors: Russell Brogan, Joseph Clarke, Anna Rue, Daniel Zapata *)

(* ======================================================================================================= *)

(* Statements *)

prog

<prog> ::= <stmtList> .

<stmtList> ::= <stmt> <stmtList> .

<stmt> ::= <block> 
         | <declare> ";" 
         | <assign> ";" 
         | <output> ";" 
         | <conditional> 
         | <iteration> .

<block> ::= "{" <stmtList> "}" .

<declare> ::= "int" "id" 
            | "bool" "id" .

<assign> ::= "id" "=" <expression> .

<output> ::= "print" "(" <expression> ")" .

<conditional> ::= "if" "(" <expression> ")" <block> 
                | "if" "(" <expression> ")" <block> "else" <block> .

<iteration> ::= "while" "(" <expression> ")" <block> 
              | "for" "(" <assign> ";" <expression> ";" <loopIncrement> ")" <block> .

<loopIncrement> ::= <assign> 
                | <increment> .

(* Boolean Expressions *)

<expression> ::= <disjunction> .

<disjunction> ::= <disjunction> "||" <conjunction> 
                | <conjunction> .

<conjunction> ::= <conjunction> "&&" <equality> 
                | <equality> .

<equality> ::= <equality> "==" <relational> 
             | <equality> "!=" <relational> 
             | <relational> .

<relational> ::= <relational> "<" <additive> 
               | <relational> ">" <additive> 
               | <additive> .

(* Integer Expressions *)

<additive> ::= <additive> "+" <multiplicative> 
             | <additive> "-" <multiplicative> 
             | <multiplicative> .

<multiplicative> ::= <multiplicative> "*" <negation> 
                   | <multiplicative> "/" <negation> 
                   | <multiplicative> "%" <negation> 
                   | <negation> .

<negation> ::= "!" <negation> 
             | "~" <negation> 
             | <exponent> .

<exponent> ::= <absolute> "^" <exponent> 
             | <absolute> .

<absolute> ::= "abs" "(" <absolute> ")" 
             | <base> .

<base> ::= "(" <expression> ")" | <increment> 
         | "id" | "boolean" | "integer" .

<increment> ::= "id" "++" 
              | "id" "--" 
              | "++" "id" 
              | "--" "id" .
